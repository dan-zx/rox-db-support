/*
 * Copyright 2014-2015 Daniel Pedraza-Arcega
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
apply plugin: 'java'
apply plugin: 'eclipse'

group = 'com.rox'
version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint']

repositories {
    mavenCentral()
    maven { url 'https://dl.dropboxusercontent.com/u/1995295/m2repository' }
    maven { url 'http://m2.neo4j.org/content/groups/public' }
}

ext {
    neo4jVersion  = '2.2.4'
    slf4jVersion  = '1.7.12'
    springVersion = '4.1.7.RELEASE'
}

dependencies {
    // LOGGING
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    runtime 'org.codehaus.groovy:groovy:2.4.4'

     // SPRING
    compile ("org.springframework:spring-context:${springVersion}") {
        exclude module: 'commons-logging'
    }
    compile "org.springframework:spring-jdbc:${springVersion}"
    compile "org.springframework:spring-tx:${springVersion}"
    compile 'javax.inject:javax.inject:1'

    // FOURSQUARE
    compile 'com.foursquare4j:foursquare4j:0.1'

    // DATABASE
    compile 'org.neo4j:neo4j-jdbc:2.1.4'
    compile "org.neo4j:neo4j:${neo4jVersion}"

    // TEST
    testCompile 'com.squareup.okhttp:mockwebserver:2.4.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.1.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.neo4j:neo4j-io:${neo4jVersion}:tests@jar"
    testCompile "org.neo4j:neo4j-kernel:${neo4jVersion}:tests@jar"
    testCompile "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
}

task copyLibs(type: Copy) {
    from configurations.compile
    into 'build/libs/libs'
    from configurations.runtime
    into 'build/libs/libs'
}

task copyConf(type: Copy) {
    from 'conf'
    into 'build/libs'
}

assemble.dependsOn copyConf
assemble.dependsOn copyLibs

jar {
    manifest {
        attributes (
            'Main-Class': 'com.grayfox.server.Program',
            'Class-Path':  '. ' + configurations.compile.collect { 'libs/' + it.getName() }.join(' ') + ' ' + configurations.runtime.collect { 'libs/' + it.getName() }.join(' ')
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}